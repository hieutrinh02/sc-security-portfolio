# RAAC Core Contracts - Findings Report

# Table of contents
- ## [Contest Summary](#contest-summary)
- ## [Results Summary](#results-summary)
- ## High Risk Findings
    - ### [H-01. Incorrect amount calculation in `veRAACToken::increase` leads to inflated veRAAC token minting and voting power](#H-01)
- ## Medium Risk Findings
    - ### [M-01. Identical proposal parameters cause operation ID collision](#M-01)
- ## Low Risk Findings
    - ### [L-01. Incorrect collateral liquidated in `LiquidationFinalized` event due to `getUserCollateralValue` call after NFT deletion](#L-01)
    - ### [L-02. Incorrect fee share initialization leads to excessive fund distribution](#L-02)


# <a id='contest-summary'></a>Contest Summary

### Sponsor: Regnum Aurum Acquisition Corp

### Dates: Feb 3rd, 2025 - Feb 24th, 2025

[See more contest details here](https://codehawks.cyfrin.io/c/2025-02-raac)

# <a id='results-summary'></a>Results Summary

### Number of findings:
- High: 1
- Medium: 1
- Low: 2


# High Risk Findings

## <a id='H-01'></a>H-01. Incorrect amount calculation in `veRAACToken::increase` leads to inflated veRAAC token minting and voting power            



## Summary

In `contracts::core::tokens::veRAACToken.sol`, a miscalculation in the `increase` function causes the voting power calculation to use an inflated amount, leading to excessive voting power allocation. This occurs because `userLock.amount + amount` is passed to `calculateAndUpdatePower`, whereas `userLock.amount` has already been updated earlier in the function. This results in users receiving twice the intended increasing veRAAC tokens and voting power, potentially inflating their rewards and compromising governance integrity.

## Vulnerability Details

In `increase`, the function increases the amount of locked RAAC tokens. After increasing lock using the `LockManager` library, the incorrect amount of RAAC tokens is used to calculate and update voting power.

In `contracts/core/tokens/veRAACToken.sol#L251-L262`:

```javascript
    function increase(uint256 amount) external nonReentrant whenNotPaused {
        // Increase lock using LockManager
        _lockState.increaseLock(msg.sender, amount);
        _updateBoostState(msg.sender, locks[msg.sender].amount);

        // Update voting power
        LockManager.Lock memory userLock = _lockState.locks[msg.sender];
        (int128 newBias, int128 newSlope) = _votingState.calculateAndUpdatePower(
            msg.sender,
@>          userLock.amount + amount, // ❌ Incorrect: `userLock.amount` is already updated
            userLock.end
        );
```

We can see that `amount` is added into `userLock.amount`, which results in double counting because `userLock.amount` already includes `amount` after `increaseLock` is called.

In `contracts/libraries/governance/LockManager.sol#L152-L166`:

```javascript
    function increaseLock(
        LockState storage state,
        address user,
        uint256 additionalAmount
    ) internal {
        Lock storage lock = state.locks[user];
        if (!lock.exists) revert LockNotFound();
        if (lock.end <= block.timestamp) revert LockExpired();
        
        // Maximum lock amount
        if (lock.amount + additionalAmount > state.maxLockAmount) revert AmountExceedsLimit();
        // Maximum total locked amount
        // if (state.totalLocked + additionalAmount > state.maxTotalLocked) revert AmountExceedsLimit();
        
@>      lock.amount += additionalAmount; // `additionalAmount` is already added into user's lock position here
```

A brief PoC is shown below:

Place the following code into `test/unit/core/tokens/veRAACToken.test.js`, in `describe("veRAACToken")` scope and in `describe("Lock Mechanism")` scope:

```javascript
    it("should allow users to increase lock amount with inflated voting power", async () => {
            const initialAmount = ethers.parseEther("1000");
            const additionalAmount = ethers.parseEther("500");
            const duration = 365 * 24 * 3600; // 1 year

            // For simplicity, we will assume `users[0]` locks and increases at the same time.
            await veRAACToken.connect(users[0]).lock(initialAmount, duration);
            await veRAACToken.connect(users[0]).increase(additionalAmount);

            // We also let `users[1]` lock `initialAmount + additionalAmount` amount of RAAC tokens, equal to the total amount of tokens that `users[0]` has locked.
            await veRAACToken.connect(users[1]).lock(initialAmount + additionalAmount, duration);

            const user_0_power = await veRAACToken.balanceOf(users[0].address);
            const user_1_power = await veRAACToken.balanceOf(users[1].address);

            // Since `users[0]` and `users[1]` have the same amount of RAAC tokens locked for the same duration, they should have the same power.
            console.log("User 0 Power:", user_0_power);
            console.log("User 1 Power:", user_1_power);

            // But `users[0]` has more due to the incorrect amount of RAAC tokens used to calculate power
            expect(user_0_power).to.be.gt(user_1_power);
        })
```

Run `npx hardhat test --grep "should allow users to increase lock amount with inflated voting power"`

```javascript
    veRAACToken
    Lock Mechanism
User 0 Power: 500000000000000000000n
User 1 Power: 375000000000000000000n
      ✔ should allow users to increase lock amount with inflated voting power


  1 passing (2s)
```

## Impact

Users can unfairly increase their veRAAC token balance and voting power, allowing them to inflate their rewards and influence decisions beyond their actual amount of locked RAAC tokens.

## Tools Used

Manual Review

## Recommendations

Modify the function to ensure the correct amount is passed:

```diff
    function increase(uint256 amount) external nonReentrant whenNotPaused {
        // Increase lock using LockManager
        _lockState.increaseLock(msg.sender, amount);
        _updateBoostState(msg.sender, locks[msg.sender].amount);

        // Update voting power
        LockManager.Lock memory userLock = _lockState.locks[msg.sender];
        (int128 newBias, int128 newSlope) = _votingState.calculateAndUpdatePower(
            msg.sender,
-           userLock.amount + amount,
+           userLock.amount, // ✅ Correct: Only use the updated `userLock.amount`
            userLock.end
        );
```

    
# Medium Risk Findings

## <a id='M-01'></a>M-01. Identical proposal parameters cause operation ID collision            



## Summary

In `contracts::core::governance::proposals::Governance.sol`, the `_queueProposal` function computes an operation ID using a hash of the proposal's targets, values, calldata, and description hash. If two proposals have the same targets, values, calldata, and description hash, they will generate identical operation IDs, causing a collision. This prevents the second proposal from being properly queued and executed, leading to governance deadlocks.

## Vulnerability Details

The issue arises in `_queueProposal`, where the operation ID is calculated using the proposal's targets, values, calldatas and description hash.

In `contracts::core::governance::proposals::Governance.sol#L487-L497`:

```javascript
    function _queueProposal(uint256 proposalId) internal {
        ProposalCore storage proposal = _proposals[proposalId];
        
        bytes32 salt = proposal.descriptionHash;
        bytes32 id = _timelock.hashOperationBatch(
            proposal.targets,
            proposal.values,
            proposal.calldatas,
            bytes32(0),
            salt
        );
```

Those parameters will be used in `hashOperationBatch` to generate a unique operation ID.

In `contracts::core::governance::proposals::TimelockController.sol#L318-L326`:

```javascript
    function hashOperationBatch(
        address[] calldata targets,
        uint256[] calldata values,
        bytes[] calldata calldatas,
        bytes32 predecessor,
        bytes32 salt
    ) public pure returns (bytes32) {
        return keccak256(abi.encode(targets, values, calldatas, predecessor, salt));
    }
```

This means two proposals with identical `targets`, `values`, `calldatas`, and `descriptionHash` will result in the same `id`. The `_operations` mapping in `TimelockController` contract stores the operation information including its timestamp and executed status. As a result, when a second identical proposal is proposed, the operation information will be incorrect since an entry with the same ID already exists, preventing it from being properly queued and executed.

## Impact

Having the same operation ID can cause a number of problems. Some of them are:

- Governance Deadlock: If a proposal is queued, subsequent identical proposals cannot be queued, limiting governance flexibility.

- Proposal Reusability Issue: Some proposals may need to be reintroduced with the same parameters, but the system will block them due to the collision.

## Tools Used

Manual Review

## Recommendations

Include a proposal-specific nonce in the hash calculation to ensure uniqueness. Alternatively, consider incorporating the unique `proposalId` instead of `descriptionHash`. 

# Low Risk Findings

## <a id='L-01'></a>L-01. Incorrect collateral liquidated in `LiquidationFinalized` event due to `getUserCollateralValue` call after NFT deletion            



## Summary

In `contracts::core::pools::LendingPool::LendingPool.sol`, the `finalizeLiquidation` function emits the `LiquidationFinalized` event, including the user's collateral liquidated, after deleting `user.nftTokenIds`. This results in `getUserCollateralValue(userAddress)` returning `0`, misleading off-chain services relying on event emission.

## Vulnerability Details

In `finalizeLiquidation`, the function processes liquidation by transferring NFTs, updating debt balances, and emitting the `LiquidationFinalized` event. The last parameter of this event is `collateralLiquidated`, which is the amount of collateral liquidated. Therefore, the function calls `getUserCollateralValue(userAddress)`, which iterates over `user.nftTokenIds` to compute collateral value. However, since `getUserCollateralValue(userAddress)` is called after `user.nftTokenIds` has been deleted, the function returns `0` instead of the actual collateral value.

In `contracts/core/pools/LendingPool/LendingPool.sol#L511-L535`:

```JavaScript
    isUnderLiquidation[userAddress] = false;
    liquidationStartTime[userAddress] = 0;

    // Transfer NFTs to Stability Pool
    for (uint256 i = 0; i < user.nftTokenIds.length; i++) {
        uint256 tokenId = user.nftTokenIds[i];
        user.depositedNFTs[tokenId] = false;
        raacNFT.transferFrom(address(this), stabilityPool, tokenId);
    }
    delete user.nftTokenIds; // <-- Causes incorrect event data

.
.
.

    // Call getUserCollateralValue after deleting NFT data
    emit LiquidationFinalized(stabilityPool, userAddress, userDebt, getUserCollateralValue(userAddress));
```

Since `getUserCollateralValue` retrieves NFT prices by iterating `user.nftTokenIds`, an empty array results in a `0` collateral value in the emitted event, leading to inaccurate data.

In `contracts/core/pools/LendingPool/LendingPool.sol#L561-L572`:

```JavaScript
    function getUserCollateralValue(address userAddress) public view returns (uint256) {
        UserData storage user = userData[userAddress];
        uint256 totalValue = 0;

@>      for (uint256 i = 0; i < user.nftTokenIds.length; i++) {
            uint256 tokenId = user.nftTokenIds[i];
            uint256 price = getNFTPrice(tokenId);
            totalValue += price;
        }

        return totalValue;
    }
```

## Impact

Frontend or other off-chain services may display incorrect values, potentially misleading users.

## Tools Used

Manual Review

## Recommendations

Capture the user's collateral value before deleting `user.nftTokenIds`:

```diff
    // Transfer NFTs to Stability Pool
    for (uint256 i = 0; i < user.nftTokenIds.length; i++) {
        uint256 tokenId = user.nftTokenIds[i];
        user.depositedNFTs[tokenId] = false;
        raacNFT.transferFrom(address(this), stabilityPool, tokenId);
    }
+   uint256 collateralValue = getUserCollateralValue(userAddress);
    delete user.nftTokenIds; // Delete after storing collateral value

.
.
.

-   emit LiquidationFinalized(stabilityPool, userAddress, userDebt, getUserCollateralValue(userAddress));
+   emit LiquidationFinalized(stabilityPool, userAddress, userDebt, collateralValue);
```

## <a id='L-02'></a>L-02. Incorrect fee share initialization leads to excessive fund distribution            



## Summary

In `contracts::core::collectors::FeeCollector.sol` , the `FeeCollector` contract incorrectly assigns fee share values in `feeTypes[6]` (Swap Tax) and `feeTypes[7]` (NFT Royalty Fees). The fee shares in basis points are initialized 10 times higher than the protocol rules, causing the protocol to distribute significantly more funds than intended.

## Vulnerability Details

The `FeeCollector` contract's constructor uses `_initializeFeeTypes` internal function to initialize fee types with protocol rules. In `_initializeFeeTypes`, fee shares are set using a basis points system (BASIS\_POINTS = 10000). However, the values in `feeTypes[6]` and `feeTypes[7]` appear to be mistakenly set at 500 for 0.5% instead of 50, and 1000 for 1.0% instead of 100.

In `contracts/core/collectors/FeeCollector.sol#L379-L393`:

```javascript
    // Buy/Sell Swap Tax (2% total)
    feeTypes[6] = FeeType({
        veRAACShare: 500,     // 0.5% (Incorrect, should be 50)
        burnShare: 500,       // 0.5% (Incorrect, should be 50)
        repairShare: 1000,    // 1.0% (Incorrect, should be 100)
        treasuryShare: 0
    });
        
    // NFT Royalty Fees (2% total)
    feeTypes[7] = FeeType({
        veRAACShare: 500,     // 0.5% (Incorrect, should be 50)
        burnShare: 0,
        repairShare: 1000,    // 1.0% (Incorrect, should be 100)
        treasuryShare: 500    // 0.5% (Incorrect, should be 50)
    });
```

## Impact

Each affected category will receive 10x the intended share of funds, causing the protocol to distribute significantly more funds than intended. 

More specifically, in `contracts/core/collectors/FeeCollector.sol#L455-L456`: 

```javascript
    uint256 remainder = totalFees - (shares[0] + shares[1] + shares[2] + shares[3]);
    if (remainder > 0) shares[3] += remainder;
```

Here, the fee remainder is added to `shares[3]` (the treasury share). Since the other shares are inflated, the treasury share will be significantly decreased.

## Tools Used

Manual Review

## Recommendations

Correct the fee share values in `feeTypes[6]` and `feeTypes[7]`:

```diff
    // Buy/Sell Swap Tax (2% total)
    feeTypes[6] = FeeType({
-       veRAACShare: 500,     // 0.5%
+       veRAACShare: 50,     // 0.5%
-       burnShare: 500,       // 0.5%
+       burnShare: 50,       // 0.5%
-       repairShare: 1000,    // 1.0%
+       repairShare: 100,    // 1.0%
        treasuryShare: 0
    });
        
    // NFT Royalty Fees (2% total)
    feeTypes[7] = FeeType({
-       veRAACShare: 500,     // 0.5%
+       veRAACShare: 50,     // 0.5%
        burnShare: 0,
-       repairShare: 1000,    // 1.0%
+       repairShare: 100,    // 1.0%
-       treasuryShare: 500    // 0.5%
+       treasuryShare: 50    // 0.5%
    });
```



